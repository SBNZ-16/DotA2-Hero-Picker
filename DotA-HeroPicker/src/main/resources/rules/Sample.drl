package com.sample
 
import com.heropicker.facts.HeroBannedFact;
import com.heropicker.facts.AllyHeroPickedFact;
import com.heropicker.facts.EnemyHeroPickedFact;
import com.heropicker.facts.UpdateScoresFact;
import com.heropicker.facts.HeroPreferredFact;
import com.heropicker.facts.RolePreferredFact;
import com.heropicker.facts.LanePreferredFact;
import com.heropicker.model.PickedAllyHeroes;
import com.heropicker.model.HeroRecommendation;
import com.heropicker.model.LaneStats;
import com.heropicker.model.Hero;


global com.heropicker.model.HeroDatabase heroDatabase;
global com.heropicker.model.HeroRecommendationList heroRecommendationList;
global PickedAllyHeroes pickedAllyHeroes;

 
rule "Ally hero picked"
	agenda-group "hero-statistics"
	when
		$picked : AllyHeroPickedFact()
	then
		heroRecommendationList.removeHeroRecommendationByHeroId($picked.getHeroId());
		pickedAllyHeroes.addHero($picked.getHeroId());
end

rule "Enemy hero picked"
	agenda-group "hero-statistics"
	when
		$picked : EnemyHeroPickedFact()
	then
		heroRecommendationList.removeHeroRecommendationByHeroId($picked.getHeroId());
		heroRecommendationList.changeScoresByHeroDisadvantage($picked.getHeroId(), 1.5);
end

rule "Enemy hero picked is expected to show in my lane"
	agenda-group "hero-statistics"
	when
		$picked : EnemyHeroPickedFact(goingToFaceInLane == true)
	then
		heroRecommendationList.changeScoresByHeroDisadvantage($picked.getHeroId(), 3);
end

rule "Hero banned"
	agenda-group "hero-statistics"
	when
		$banned : HeroBannedFact()
	then
		heroRecommendationList.removeHeroRecommendationByHeroId($banned.getHeroId());
end



rule "Nuker role"
	agenda-group "team-composition"
	salience -1
	when
		AllyHeroPickedFact()
		$count : Long() from accumulate ($hero : Hero(roles contains "Nuker") from pickedAllyHeroes.heroes, count($hero))
	then
		double penalty = (1 - (double)$count) * 0.1;
		insert(new UpdateScoresFact(penalty + 1, 0, null, "Nuker", null));
end

rule "Escape role"
	agenda-group "team-composition"
	salience -1
	when
		AllyHeroPickedFact()
		$count : Long() from accumulate ($hero : Hero(roles contains "Escape") from pickedAllyHeroes.heroes, count($hero))
	then
		double penalty = (1 - (double)$count) * 0.05;
		insert(new UpdateScoresFact(penalty + 1, 0, null, "Escape", null));
end

rule "Support role"
	agenda-group "team-composition"
	salience -1
	when
		AllyHeroPickedFact()
		$count : Long() from accumulate ($hero : Hero(roles contains "Support") from pickedAllyHeroes.heroes, count($hero))
	then
		double penalty = (2 - (double)$count) * 0.1;
		insert(new UpdateScoresFact(penalty + 1, 0, null, "Support", null));
end

rule "Initiator role"
	agenda-group "team-composition"
	salience -1
	when
		AllyHeroPickedFact()
		$count : Long() from accumulate ($hero : Hero(roles contains "Initiator") from pickedAllyHeroes.heroes, count($hero))
	then
		double penalty = (1.5 - (double)$count) * 0.1;
		insert(new UpdateScoresFact(penalty + 1, 0, null, "Initiator", null));
end

rule "Carry role"
	agenda-group "team-composition"
	salience -1
	when
		AllyHeroPickedFact()
		$count : Long() from accumulate ($hero : Hero(roles contains "Carry") from pickedAllyHeroes.heroes, count($hero))
	then
		double penalty = (2 - (double)$count) * 0.1;
		insert(new UpdateScoresFact(penalty + 1, 0, null, "Carry", null));
end

rule "Jungler role"
	agenda-group "team-composition"
	salience -1
	when
		AllyHeroPickedFact()
		$count : Long() from accumulate ($hero : Hero(roles contains "Jungler") from pickedAllyHeroes.heroes, count($hero))
	then
		double penalty = (0.5 - (double)$count) * 0.05;
		insert(new UpdateScoresFact(penalty + 1, 0, null, "Jungler", null));
end

rule "Durable role"
	agenda-group "team-composition"
	salience -1
	when
		AllyHeroPickedFact()
		$count : Long() from accumulate ($hero : Hero(roles contains "Durable") from pickedAllyHeroes.heroes, count($hero))
	then
		double penalty = (1.5 - (double)$count) * 0.1;
		insert(new UpdateScoresFact(penalty + 1, 0, null, "Durable", null));
end

rule "Disabler role"
	agenda-group "team-composition"
	salience -1
	when
		AllyHeroPickedFact()
		$count : Long() from accumulate ($hero : Hero(roles contains "Disabler") from pickedAllyHeroes.heroes, count($hero))
	then
		double penalty = (2 - (double)$count) * 0.05;
		insert(new UpdateScoresFact(penalty + 1, 0, null, "Disabler", null));
end

rule "Pusher role"
	agenda-group "team-composition"
	salience -1
	when
		AllyHeroPickedFact()
		$count : Long() from accumulate ($hero : Hero(roles contains "Disabler") from pickedAllyHeroes.heroes, count($hero))
	then
		double penalty = (1.5 - (double)$count) * 0.1;
		insert(new UpdateScoresFact(penalty + 1, 0, null, "Pusher", null));
end



rule "Hero preferred"
	agenda-group "preferences"
	when
		HeroPreferredFact($heroId : heroId)
	then
		insert(new UpdateScoresFact(1.3, 0, $heroId, null, null));
end

rule "Role preferred"
	agenda-group "preferences"
	when
		RolePreferredFact($role : role)
	then
		insert(new UpdateScoresFact(1.3, 0, null, $role, null));
end

rule "Lane preferred"
	agenda-group "preferences"
	when
		LanePreferredFact($lane : lane)
	then
		insert(new UpdateScoresFact(1, 0, null, null, $lane));
end

rule "Update score by heroId"
	agenda-group "preferences"
	when
		$updateScore : UpdateScoresFact($heroId : heroId, $role : role, $lane : lane)
		$heroRecommendation : HeroRecommendation(hero.heroId==$heroId) from heroRecommendationList.getHeroRecommendations()
	then
		$heroRecommendation.updateScore($updateScore.getMultiplyScale(), $updateScore.getAddValue());
end

rule "Update score by role"
	agenda-group "preferences"
	when
		$updateScore : UpdateScoresFact($heroId : heroId, $role : role, $lane : lane)
		$heroRecommendation : HeroRecommendation(hero.roles contains $role) from heroRecommendationList.getHeroRecommendations()
	then
		$heroRecommendation.updateScore($updateScore.getMultiplyScale(), $updateScore.getAddValue());
end

rule "Update score by lane"
	agenda-group "preferences"
	when
		$updateScore : UpdateScoresFact($heroId : heroId, $role : role, $lane : lane)
		$heroRecommendation : HeroRecommendation($hero : hero) from heroRecommendationList.getHeroRecommendations()
		$laneStats: LaneStats(name == $lane) from $hero.lanes
	then
		double laneEffectiveness = ($laneStats.getWinrate() * $laneStats.getPresence());
		$heroRecommendation.updateScore(laneEffectiveness*$updateScore.getMultiplyScale(),
		 $updateScore.getAddValue());
end
