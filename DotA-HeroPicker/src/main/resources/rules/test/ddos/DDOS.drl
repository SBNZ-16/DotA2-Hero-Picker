package rules.ddos;

import com.heropicker.facts.ddos.IpAccessFact;



declare window LatestIpAccessFact
	IpAccessFact() over window:length(1)
end

declare window TimeWindow
	IpAccessFact() over window:time(1s)
end

rule "Too many requests by a single ip address"
salience ($total*2)
    when
        $ip1: IpAccessFact($ipAddress: ipAddress, analysed == false) from window LatestIpAccessFact
        $total: Number(intValue >= 3) from accumulate(
            $ip2: IpAccessFact(
                ipAddress.equals($ipAddress)
            ) from window TimeWindow,
            count($ip2)
        )
    then
		System.out.println("DDOS from same IP spotted");
		modify($ip1) {setAnalysed(true);}
end

rule "Too many requests"
salience ($total)
    when
		$ip1: IpAccessFact($ipAddress: ipAddress, analysed == false) from window LatestIpAccessFact
        $total: Number(intValue >= 3*2) from accumulate(
            $ip2: IpAccessFact() from window TimeWindow,
            count($ip2)
        )
    then
		System.out.println("DDOS from different IPs");
		modify($ip1) {setAnalysed(true);}
end


